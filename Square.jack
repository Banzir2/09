// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/09/Square/Square.jack

/** Implements a graphical square. */
class Square {

   field int x, y; // screen location of the square's top-left corner
   field int size; // length of this square, in pixels

   /** Constructs a new square with a given location and size. */
   constructor Square new(int Ax, int Ay, int Asize) {
      let x = Ax;
      let y = Ay;
      let size = Asize;
      do draw();
      return this;
   }

   /** Disposes this square. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /** Draws the square on the screen. */
   method void draw() {
      do Screen.setColor(true);
      do Screen.drawRectangle(x, y, x + size, y + size);
      return;
   }

   /** Erases the square from the screen. */
   method void erase() {
      do Screen.setColor(false);
      do Screen.drawRectangle(x, y, x + size, y + size);
      return;
   }

   method void move(int dx, int dy) {
      do Screen.setColor(false);
      do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
      let y = y + dy;
      let x = x + dx;
      if (x < 0) {
         let x = 0;
      }
      if (y < 0) {
         let y = 0;
      }
      if (x > 512 - size) {
         let x = 512 - size;
      }
      if (y > 256 - size) {
         let y = 256 - size;
      }
      do Screen.setColor(true);
      do Screen.drawRectangle(x, y, x + size, y + 1);
      return;
   }
}
